plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'maven-publish'
    id 'idea'
    id "com.matthewprenger.cursegradle" version "1.4.+"
    id "com.modrinth.minotaur" version "2.+"
}

archivesBaseName = rootProject.archivesBaseName + '-fabric'
project.ext.mixin_refmap = "${archivesBaseName}-refmap.json"

repositories {
    maven {
        name = 'TerraformersMC'
        url = 'https://maven.terraformersmc.com/releases/'
        content {
            includeGroup 'com.terraformersmc'
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${project.parchment_minecraft_version}:${project.parchment_mappings_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    modCompileOnly "com.terraformersmc:modmenu:${mod_menu_version}"
    modLocalRuntime "com.terraformersmc:modmenu:${mod_menu_version}"

    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":Common")
}

loom {
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}


processResources {
    from project(":Common").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }

    filesMatching("mixins.*.json") {
        expand "mixin_refmap": project.ext.mixin_refmap
    }
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}

curseforge {

    def curseforgeToken = System.getenv("CURSEFORGE_TOKEN")
    if (curseforgeToken) {
        apiKey = curseforgeToken
    }

    project {
        id = project.curseforge_project_id
        changelogType = "markdown"
        changelog = project.ext.changelog
        releaseType = project.release_type
        "${project.supported_versions}".split(',').each {
            addGameVersion "${it}"
        }
        addGameVersion "Fabric"
        addGameVersion "Java 17"

        mainArtifact(remapJar) {
            displayName = "[Fabric] Transmog $project.version"
        }
        addArtifact(sourcesJar)
        relations {
            requiredDependency("fabric-api")
            optionalDependency("modmenu")
        }

        afterEvaluate {
            uploadTask.dependsOn("remapJar")
        }
    }

    options {
        forgeGradleIntegration = false
    }
}

modrinth {
    projectId = project.modrinth_project_id
    versionName = "[Fabric] Transmog $project.version"
    versionNumber = "fabric-${project.version}"
    versionType = project.release_type
    changelog = project.ext.changelog
    uploadFile = remapJar
    additionalFiles = [sourcesJar]
    gameVersions = "${project.supported_versions}".split(',') as List<String>
    loaders = ["fabric"]
    dependencies {
        required.project "fabric-api"
        optional.project "modmenu"
    }
}